import java.time.ZonedDateTime
import java.time.format.DateTimeFormatter

apply plugin: 'org.ajoberstar.grgit'

tasks.named('processResources') {
    dependsOn 'generateGitProperties'
}

tasks.register('generateGitProperties') {
    // 不检查是否最新，因为生成的文件中有 build.time，永远不可能最新
    doLast {
        def properties = ''

        def commit = grgit?.head()
        if (commit == null) {
            logger.warn("当前项目不存在 Git 仓库或仓库中没有 Commit")
        } else {
            def formatter = DateTimeFormatter.ofPattern("yyyy-MM-dd'T'HH:mm:ssZ")
            properties = """\
                git.branch=${grgit.branch.current().name}
                git.build.time=${formatter.format(ZonedDateTime.now())}
                git.commit.id=${commit.id}
                git.commit.id.abbrev=${commit.abbreviatedId}
                git.commit.time=${formatter.format(commit.dateTime)}
                """.stripIndent()
        }

        copy {
            from resources.text.fromString(properties).asFile('UTF-8')
            into processResources.destinationDir
            rename { 'git.properties' }
        }
    }
}
